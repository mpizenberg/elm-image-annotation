-- This Source Code Form is subject to the terms of the Mozilla Public
-- License, v. 2.0. If a copy of the MPL was not distributed with this
-- file, You can obtain one at http://mozilla.org/MPL/2.0/


module Pointer
    exposing
        ( Event(..)
        , Pointer
        , Track(..)
        , updateTrack
        , offset
        , touchOffsetOn
        , movement
        , offsetOn
        , movementOn
        , attributes
        , noToolAttributes
        , toolAttributes
        , askTime
        )

{-| This module aims at giving helper functions to deal with pointer events.

@docs Event, Pointer, Track, updateTrack, offset, movement
@docs offsetOn, touchOffsetOn, movementOn, attributes, noToolAttributes, toolAttributes
@docs askTime
-}

import Html as H exposing (Html)
import Html.Events as HE
import Helpers.Events as HPE
import Tool exposing (Tool)
import Time exposing (Time)
import Task exposing (Task)
import OpenSolid.Geometry.Types exposing (Point2d(..))


{-| Events generated by a pointer device.
-}
type Event
    = Down
    | Move
    | Up
    | Cancel


{-| A Pointer.
-}
type alias Pointer =
    { event : Event
    , offsetX : Float
    , offsetY : Float
    , movementX : Float
    , movementY : Float
    }


{-| Track the first and last pointer events.
-}
type Track
    = None
    | Started Pointer
    | Moved Pointer Pointer


{-| Update a record track with a new pointer event.
-}
updateTrack : Pointer -> Track -> Track
updateTrack pointer track =
    case ( pointer.event, track ) of
        ( Down, _ ) ->
            Started pointer

        ( Move, Started start ) ->
            Moved start pointer

        ( Move, Moved start _ ) ->
            Moved start pointer

        _ ->
            None


{-| Returns (offsetX, offsetY).
-}
offset : Pointer -> ( Float, Float )
offset pointer =
    ( pointer.offsetX, pointer.offsetY )


{-| Returns (movementX, movementY).
-}
movement : Pointer -> ( Float, Float )
movement pointer =
    ( pointer.movementX, pointer.movementY )



-- HTML POINTER ATTRIBUTES ###########################################


{-| Returns offsetOn attribute.
-}
offsetOn : String -> Event -> (Pointer -> msg) -> (( Float, Float ) -> ( Float, Float )) -> H.Attribute msg
offsetOn eventName event tagger transform =
    HPE.offsetOn eventName <| tagger << fromOffset event << transform


{-| Returns touchOffsetOn attribute.
-}
touchOffsetOn : String -> Event -> (Pointer -> msg) -> (( Float, Float ) -> ( Float, Float )) -> H.Attribute msg
touchOffsetOn eventName event tagger transform =
    (tagger << fromOffset event << transform)
        |> HPE.computedTouchOffsetOn eventName


{-| Returns movementOn attribute.
-}
movementOn : String -> Event -> (Pointer -> msg) -> (( Float, Float ) -> ( Float, Float )) -> H.Attribute msg
movementOn eventName event tagger transform =
    HPE.movementOn eventName <| tagger << fromMovement event << transform


{-| Returns a list of attribute messages with useful mouse events listeners.
-}
attributes : (Pointer -> msg) -> Tool -> Maybe Pointer -> List (H.Attribute msg)
attributes msgMaker currentTool previousPointer =
    case currentTool of
        Tool.None ->
            noToolAttributes msgMaker previousPointer

        _ ->
            toolAttributes msgMaker previousPointer


{-| Returns a list of attribute messages to detect movement when no tool is used.
-}
noToolAttributes : (Pointer -> msg) -> Maybe Pointer -> List (H.Attribute msg)
noToolAttributes msgMaker previousPointer =
    [ HPE.movementOn "mousedown" <| msgMaker << (fromOffset Down)
    , HPE.movementOn "mouseup" <| msgMaker << (fromOffset Up)
    ]
        ++ if previousPointer == Nothing then
            []
           else
            [ HPE.movementOn "mousemove" <| msgMaker << (fromMovement Move) ]


{-| Returns a list of attribute messages to detect movement when a tool is used.
-}
toolAttributes : (Pointer -> msg) -> Maybe Pointer -> List (H.Attribute msg)
toolAttributes msgMaker previousPointer =
    [ HPE.offsetOn "mousedown" <| msgMaker << (fromOffset Down)
    , HPE.computedTouchOffsetOn "touchstart" <| msgMaker << (fromOffset Down)
    , HPE.computedTouchOffsetOn "touchmove" <| msgMaker << (fromOffset Move)
    , HPE.offsetOn "mouseup" <| msgMaker << (fromOffset Up)
    , HPE.computedTouchOffsetOn "touchend" <| msgMaker << (fromOffset Up)
    ]
        ++ if previousPointer == Nothing then
            []
           else
            [ HPE.offsetOn "mousemove" <| msgMaker << (fromOffset Move)
            ]


fromOffset : Event -> ( Float, Float ) -> Pointer
fromOffset event ( offsetX, offsetY ) =
    { event = event
    , offsetX = offsetX
    , offsetY = offsetY
    , movementX = 0
    , movementY = 0
    }


fromMovement : Event -> ( Float, Float ) -> Pointer
fromMovement event ( movementX, movementY ) =
    { event = event
    , offsetX = 0
    , offsetY = 0
    , movementX = movementX
    , movementY = movementY
    }



-- HTML TIME ATTRIBUTES ##############################################


{-| Use a message maker (tagger) to create a command message giving Time.now.
-}
askTime : (Time -> msg) -> Cmd msg
askTime msgMaker =
    Task.perform msgMaker Time.now
